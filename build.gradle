buildscript {
  ext {
    kotlinVersion = '1.3.50'
    moshiVersion = '1.8.0'
    jettyVersion = '9.4.19.v20190610'
    okhttpVersion = '3.12.5'
    slf4jVersion = '1.7.25'
    logbackVersion = '1.2.3'
    kotlinLoggingVersion = '1.7.6'
    dokkaVersion = '0.9.18'
    junitVersion = '5.+'
    jsonAssert = '1.+'
    assertJVersion = '3.+'
    mockitoVersion = '2.+'
    gradleRioVersion = '2019.4.1'
    bintrayVersion = '1.+'
    spotlessVersion = '3.+'
  }
  repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayVersion"
    classpath "edu.wpi.first:GradleRIO:$gradleRioVersion"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessVersion"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"
  }
}

configure(subprojects) {
  group = 'org.strykeforce.thirdcoast'
  version = '19.4.1'

  apply plugin: 'java-library'
  apply plugin: 'idea'

  repositories {
    jcenter()
  }

  dependencies {
    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
  }

  idea {
    module {
      downloadJavadoc = true
      downloadSources = true
    }
  }

  test {
    useJUnitPlatform()
  }
}

// applies to all sub-projects except for deadeye
configure(subprojects - project(":deadeye")) {
  apply plugin: "edu.wpi.first.GradleRIO"

  dependencies {
    compile wpi.deps.wpilib()
    compile wpi.deps.vendor.java()
  }
}
